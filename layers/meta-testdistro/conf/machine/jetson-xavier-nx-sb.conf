#@TYPE: Machine
#@NAME: Nvidia Jetson Xavier NX dev kit (eMMC) with secureboot
#@DESCRIPTION: Nvidia Jetson Xavier NX development kit with QSPI+eMMC
#              module with secureboot enabled
INITRAMFS_IMAGE ?= "td-initramfs"
IMAGE_FSTYPES = "tar.gz ext4.gz ext4"
KERNEL_ARGS ?= "console=ttyTCU0,115200 fbcon=map:0 loglevel=3"
TEGRA_BOARDSKU ?= "0001"
TEGRA_BUPGEN_SPECS ?= "fab=100;boardsku=0001;boardrev= \
		       fab=200;boardsku=0001;boardrev= \
		       fab=300;boardsku=0001;boardrev="
IMAGE_ROOTFS_ALIGNMENT ?= "4"
require conf/machine/include/xavier-nx.inc
EMMC_SIZE ?= "15758000128"
MACHINEOVERRIDES =. "jetson-xavier-nx-devkit-emmc:secureboot:cryptparts:"
PACKAGE_EXTRA_ARCHS_append = " jetson-xavier-nx-devkit-emmc"
PARTITION_LAYOUT_TEMPLATE = "flash_${MACHINE}_custom.xml"
ROOTFSPART_SIZE_jetson-xavier-nx-sb = "4294967296"
MACHINE_EXTRA_RDEPENDS += "tegra-boot-tools-lateboot lvm2-udevrules"
CORE_IMAGE_EXTRA_INSTALL_append = " ${@'systemd-conf-prod' if d.getVar('INIT_MANAGER') == 'systemd' else ''}"

MENDER_ROOTFS_PART_A_NUMBER_jetson-xavier-nx-sb = "APP"
MENDER_ROOTFS_PART_B_NUMBER_jetson-xavier-nx-sb = "APP_b"
MENDER_DATA_PART_NUMBER_jetson-xavier-nx-sb = "DATA"
MENDER_DATA_PART_SIZE_MB = "128"
MENDER_STORAGE_DEVICE_BASE = "/dev/mapper/"
# Reduce the rootfs size by 10% to allow for cryptfs overhead
MENDER_IMAGE_ROOTFS_SIZE_DEFAULT = "${@int(d.getVar('ROOTFSPART_SIZE')) * 9 // 10240}"
